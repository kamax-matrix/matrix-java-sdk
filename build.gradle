/*
 * matrix-java-sdk - Matrix Client SDK for Java
 * Copyright (C) 2017 Maxime Dor
 *
 * https://max.kamax.io/
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

import java.util.regex.Pattern

String gitVersion() {
    def versionPattern = Pattern.compile("v(\\d+\\.)?(\\d+\\.)?(\\d+)(-.*)?")
    ByteArrayOutputStream out = new ByteArrayOutputStream()
    exec {
        commandLine = ['git', 'describe', '--tags', '--always', '--dirty']
        standardOutput = out
    }
    def v = out.toString().replace(System.lineSeparator(), '')
    return versionPattern.matcher(v).matches() ? v.substring(1) : v
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
    }
}

plugins {
    id "com.diffplug.gradle.spotless" version "3.5.2"
}

group = 'io.kamax'
version = gitVersion()

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'org.junit.platform.gradle.plugin'

configurations {
    deployerJars
}

repositories {
    mavenCentral()
}

sourceSets {
    testInt {
        compileClasspath += sourceSets.test.compileClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath
    }
}


dependencies {
    compile 'org.slf4j:log4j-over-slf4j:1.7.25'
    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'commons-io:commons-io:2.5'
    compile 'org.apache.httpcomponents:fluent-hc:4.5.3'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'net.i2p.crypto:eddsa:0.1.0'

    testCompile "org.junit.jupiter:junit-jupiter-api:5.0.0"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.0.0"
    testCompile "com.github.tomakehurst:wiremock:2.8.0"
    testRuntimeOnly 'org.slf4j:slf4j-simple:1.7.25'

    testIntCompile sourceSets.main.output
    testIntCompile sourceSets.test.output

    testIntCompile configurations.compile
    testIntCompile configurations.testCompile

    testIntRuntime configurations.runtime
    testIntRuntime configurations.testRuntime

    deployerJars "org.apache.maven.wagon:wagon-ssh:2.2"
}

uploadArchives {
    gradle.ext.mavenUrl = System.env.MAVEN_URL + (project.version ==~ /\d+\.\d+\.\d+/ ? '/releases' : '/snapshots')
    gradle.ext.mavenUser = System.env.MAVEN_USER
    gradle.ext.mavenPassword = System.env.MAVEN_PASS
    gradle.ext.mavenPrivateKey = System.env.MAVEN_PK

    doLast {
        println "Published ${project.version} to maven repository ${gradle.mavenUrl} with user ${gradle.mavenUser}"
    }

    repositories.mavenDeployer {
        configuration = configurations.deployerJars

        repository(url: gradle.mavenUrl) {
            authentication(
                    userName: gradle.mavenUser,
                    password: gradle.mavenPassword,
                    privateKey: gradle.mavenPrivateKey
            )
        }
    }
}

spotless {
    format 'misc', {
        target '**/*.gradle', '**/.gitignore'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    java {
        eclipse().configFile 'spotless.eclipseformat.xml'
        removeUnusedImports()
        importOrderFile 'spotless.importorder'
    }
}

junitPlatform {
    enableStandardTestTask true
}

task testInt(type: Test) {
    description = "Integration tests which require a Homeserver"
    testClassesDir = sourceSets.testInt.output.classesDir
    classpath = sourceSets.testInt.runtimeClasspath
}
